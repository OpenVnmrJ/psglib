"       Varian Associates   VNMR   @(#)pge_calib 14.1 12/08/98 "
"  pge_calib<(const)>  -  this macro recalculates the coeficient "
"  which relates dac values to gradient levels"
"  It assumes that a diffusion experiment has just been run and "
"  the data processed "
"  The optional argument is the value the diffusion constant should"
"  be"

$value = 0
if ($# = 0) then
  input('ENTER THE THEORETICAL DIFFUSION CONSTANT (cm*cm/sec): '):$value
else
  $value = $1
  endif
lookup('file',curexp+'/analyze.list','diffusion','constant','is','read'):$calc
write('alpha','originally calculated diffusion constant was %g',$calc)
write('alpha','original coeficient was %g',grad_p_coef)
grad_p_coef = sqrt($calc * grad_p_coef * grad_p_coef / $value)
write('alpha','new coeficient is       %g',grad_p_coef)
$num = size('grad_p1')
$i = 1
while $i <= $num do
  grad_p1[$i] = grad_p1[$i]
  $i = $i + 1
  endwhile
grad_cw_coef = grad_p_coef
grad_cw = grad_cw
exists('grad_p_coef','parameter','systemglobal'):$e
if ($e > 0.5) then
  fsave(systemdir+'/conpar','systemglobal')
  endif
